/* SoftwareSerial example, MCU1 to MCU2. This sketch is for MCU1 - the master, sending commands to MCU2.
 * Author: D. Dubins
 * Date: 10-Dec-24

 * Description: This project illustrates how to get one MCU to send commands to another through a SoftwareSerial connection.
 * This sketch is for the first MCU (MCU1). The second MCU (MCU2) will be powered parasitically through the Vin and GND pins of MCU1.
 * This is hookup is optional though, they can be independently powered.
 * 
 * MCU1 reads an integer from the Serial Monitor, and sends a command to MCU2 through the SoftwareSerial connection.
 * MCU2 receives the command, executes a function, then reports back to MCU1 for error checking.

 * Connections: (MCU1 - MCU2)
 * Vin - Vin (or power independently)
 * GND - GND
 * Pin 2 - Pin 3 (RX - TX)
 * Pin 3 - Pin 2 (TX - RX)
*/

#include <SoftwareSerial.h>       // include the software serial library
SoftwareSerial MCU2Serial(2, 3);  // define software serial connection to MCU2 (RX:2, TX:3)

#define TIMEOUT 2000             // threshold (in milliseconds) for communications timeout
unsigned long timer = millis();  // for timer

void setup() {
  Serial.begin(57600);      // start serial connection
  MCU2Serial.begin(57600);  // start software serial connection with MCU2 (max speed for SoftwareSerial is 57600 for Uno)
  delay(1000);              // wait for both MCUs to boot up
  Serial.println("Enter number of flashes to send to MCU2.");
}

void loop() {
  int snd, rcv;               // define variables for sending and receiving data
  if (Serial.available()) {   // if user entered something on the serial monitor
    snd = Serial.parseInt();  // store it to snd
    Serial.print("Sending to MCU2: ");
    Serial.println(snd);    // send snd to the regular Serial Monitor
    MCU2Serial.print(snd);  // send snd to MCU2
    timer = millis();       // start the timer
    while (!MCU2Serial.available() && millis() - timer < TIMEOUT);  // wait for response from MCU2, with TIMEOUT limit
    if (MCU2Serial.available()) {   // if there's something waiting from MCU2Serial
      rcv = MCU2Serial.parseInt();  // store it to rcv
      Serial.print("Receiving from MCU2: ");
      Serial.println(rcv);  // send rcv to the regular Serial Monitor
      if (snd == rcv) {     // integers should match
        Serial.println("MCU2: Communication successful.");
      } else {
        Serial.println("MCU2: Communication error.");
      }
    } else {  // timeout feature
      Serial.println("MCU2: Communcation timed out.");
      MCU2Serial_clear();  // get rid of any residual data on the MCU2Serial monitor
    }
  }
}

void MCU2Serial_clear() {           // clear the serial monitor (read until empty)
  while (MCU2Serial.available()) {  // while data are available on the serial monitor
    byte i = MCU2Serial.read();     // read the data and store to i
  }
}
